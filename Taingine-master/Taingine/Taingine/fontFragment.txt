#version 330

in vec2 pass_textureCoords;

out vec4 out_colour;

uniform vec3 colour;
uniform sampler2D fontAtlas;
uniform float width; // size of the rendered section from the center of text
uniform float edge;  // size of edge used to smoothe edges or add special effects 
uniform float borderWidth;
uniform float borderEdge;
uniform vec3 borderColour;
uniform vec2 offset;

void main(void){

	// distance from center this pixel is. needs to move towards zero
	float distance = 1.0 - texture(fontAtlas, pass_textureCoords).a;
	float alpha = 1.0 - smoothstep(width, width + edge, distance);

	//The following dictates the special effect around the text, if any.
	float distance2 = 1.0 - texture(fontAtlas, pass_textureCoords + offset).a;
	float outlineAlpha = 1.0 - smoothstep(borderWidth, borderWidth + borderEdge, distance2);

	float overallAlpha = alpha + (1.0 - alpha) * outlineAlpha;
	vec3 overallColour = mix(borderColour, colour, alpha / overallAlpha);

	out_colour = vec4(overallColour, overallAlpha);

}